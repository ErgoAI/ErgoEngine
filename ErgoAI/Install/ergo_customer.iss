; Script generated by the Inno Setup Script Wizard.
; Modified from xsb.iss and flora2bundle.iss
; Double click on this file to run with inno, then build-menu, compile.
; A compiled version of Ergo must exist at the location pointed to
; by the ErgoBaseDir variable below.
; A compiled version of XSB must exist at the location of XSBBaseDir

#define MyAppName "Coherent ErgoAI"
#define ErgoAIVersion "3.0"

#define ErgoVersion "3.0"
#define StudioVersion "1.0"
#define XSBVersion "5.0"
#define MyAppVerName "Ergo  v. "+ErgoVersion+" (Philo), ErgoAI Studio "+StudioVersion+", and XSB "+XSBVersion
#define MyAppPublisher "Coherent Knowledge Systems"
#define MyAppURL "http://coherentknowledge.com/"
#define MyAppUrlName "ErgoAI Web Site.url"
#define FlrLicenseURL "http://www.apache.org/licenses/LICENSE-2.0"
#define FlrLicenseUrlName "Apache License Web Site.url"
#define XSBLicenseURL "http://www.gnu.org/licenses/old-licenses/lgpl-2.0.html"
#define XSBLicenseUrlName "LGPL 2.0 License Web Site.url"
#define InstallYear 2023

;;#define ERGOAI_DIR "{reg:HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment,ERGOAI_CUST_DIR|{pf}\Coherent\ERGOAI}"
#define ERGOAI_DIR "{reg:HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment,ERGOAI_CUST_DIR|{%USERPROFILE}\Coherent\ERGOAI}"
;; Change H:\ERGOAI as appropriate
#define ErgoBaseDir "H:\ERGOAI\ErgoEngine\ErgoAI"
#define XSBBaseDir "H:\ERGOAI\XSB"
;;#define ErgoAIBaseDir "H:\ERGOAI"

#define ergoapp   "{app}\ErgoEngine\ErgoAI"
#define xsbapp    "{app}\XSB"
#define ergoAIapp "{app}"

;; Skip this step if you are not an authorized Coherent code signer.
;;
;; This is setup in the IDE of Inno Setup
;; coherentsigntool: "C:\Program Files (x86)\Windows Kits\8.1\bin\x86\signtool.exe" sign /a /n "Coherent Knowledge Systems, LLC" /d  "ErgoAI Reasoner" $f
;; Find CertMdg on your machine (in Control Panel or c:\Windows\System32)
;; and import the certificate
;; H:\ERGOAI\ErgoEngine\ErgoAI\Install\certs\ergo-code-sign-cert.pfx
;; Password protected. ergo-code-sign-cert.pfx must be first placed at
;; H:\ERGOAI\ErgoEngine\ErgoAI\Install\certs\

[Setup]
;; Comment this section out if you are not an authorized Coherent code signer.
;;
;; This is a signtool for SHA1
;;SignTool=coherentsigntool /fd sha1 /t http://timestamp.comodoca.com/authenticode
;; This is a signtool for SHA256:
SignTool=coherentsigntool /fd sha256 /td sha256 /as /tr http://timestamp.comodoca.com/rfc3161
SignedUninstaller=yes

AppName={#MyAppName}
AppVersion={#ErgoAIVersion}
AppId={#MyAppName} {#ErgoAIVersion}

AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ChangesEnvironment=yes
DefaultDirName={#ERGOAI_DIR}-{#ErgoAIVersion}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile={#ErgoBaseDir}\Install\LICENSE_ergo
InfoBeforeFile={#ErgoBaseDir}\Install\InstallMessage_ergo
OutputBaseFilename=ergoAI_{#ErgoAIVersion}
Compression=lzma
SolidCompression=yes
PrivilegesRequired=none
TouchTime=none

VersionInfoVersion={#ErgoVersion}
VersionInfoCopyright=© The Research Foundation of SUNY, 1999 - {#InstallYear}; Coherent Knowledge Systems, 2013 - {#InstallYear}
VersionInfoCompany=Coherent Knowledge Systems

AllowRootDirectory=yes
UninstallFilesDir="{userdocs}\ErgoAI uninstaller"

MinVersion=0,5.1

; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
; On all other architectures it will install in "32-bit mode".
ArchitecturesInstallIn64BitMode=x64
; Note: We don't set ProcessorsAllowed because we want this
; installation to run on all architectures (including Itanium,
; since it's capable of running 32-bit code too).

[Types]
Name: "default"; Description: "Standard ErgoAI installation"

[Components]
Name: "base"; Description: "Base system"; Types: default; Flags: disablenouninstallwarning
;; Name: "documentation"; Description: "Documentation"; Types: default; Flags: disablenouninstallwarning

[Tasks]
Name: website; Description: "&Visit Coherent Knowledge Web site"; Components: base
Name: shortcut; Description: "&Create a desktop shortcut to run ErgoAI"; Components: base

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Messages]
BeveledLabel=ErgoAI {#ErgoVersion} © Coherent Knowledge Systems, 2013 - {#InstallYear}

[Dirs]
Name: "{#ergoAIapp}" ; Permissions: authusers-modify
Name: "{userdocs}\ErgoAI uninstaller"

[Files]
Source: "{#ErgoBaseDir}\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\ergo_sanity_check.bat"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\runergo.bat"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\runflora.bat"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\LICENSE_ergo"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\rlwrap_keywords.txt"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion 

Source: "{#ErgoBaseDir}\etc\ergo-desktop.ico"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\etc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\etc\ergo-desktop-cmd.ico"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\etc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\etc\ergo.ico"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\etc"; Components: base; Flags: ignoreversion recursesubdirs 

;; exclude flrcompiler.[PH], flrparser.[PH], flrcomposer.[PH], flrshell.[PH],
;;          flora2.[PH], flrlibman.[PH]
;; so that they won't get recompiled by accident
;; Here we exclude these files with ...* but later add the .xwam files
Source: "{#ErgoBaseDir}\*.P"; Excludes: "flrshell.*,flrcompiler.*,flrparser.*,flrcomposer.*,flora2.*,flrlibman.*,.*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\*.H"; Excludes: "flrshell.*,flrcompiler.*,flrparser.*,flrcomposer.*,flora2.*,flrlibman.*,.*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion

Source: "{#ErgoBaseDir}\.ergo_buildinfo.P"; Excludes: "CVS"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\AT\*.flr"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\AT"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\AT\include\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\AT\include"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\AT\prolog\*.P"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\AT\prolog"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\closure\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\closure"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\closure\*.fli"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\closure"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\closure\*.inc"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\closure"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\debugger\*.dat"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\debugger"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\debugger\*.P"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\debugger"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\debugger\*.in"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\debugger"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\demos\*.flr"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\demos"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\demos\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\demos"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\demos\sgml\*.flr"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\demos\sgml"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\demos\xpath\*.flr"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\demos\xpath"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\demos\sgml\expectedoutput"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\demos\expectedoutput"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ErgoBaseDir}\demos\xpath\expectedoutput"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\xpath\expectedoutput"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ErgoBaseDir}\demos\.ergo_aux_files\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\demos\.ergo_aux_files"; Components: base; Flags: ignoreversion recursesubdirs 

;; Not distributing Ergo manuals any more
;;Source: "{#ErgoBaseDir}\docs\*.pdf"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\docs"; Components: documentation; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\emacs\ergo.el*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\emacs"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\emacs\README"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\emacs"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\flrincludes\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\flrincludes"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\genincludes\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\genincludes"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\headerinc\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\headerinc"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\includes\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\includes"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\includes\*.fli"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\includes"; Components: base; Flags: ignoreversion recursesubdirs 


Source: "{#ErgoBaseDir}\lib\*.flr"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\lib"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\lib\include\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\lib\include"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\lib\.ergo_aux_files\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\lib\.ergo_aux_files"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\libinc\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\libinc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\ergo_libinc\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_libinc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\pkgsinc\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\pkgsinc"; Components: base; Flags: ignoreversion recursesubdirs 


Source: "{#ErgoBaseDir}\cc\*.P"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\cc\prolog2hilog.*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\cc\flora_ground.*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\cc\flrcc_init.P"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\cc\flora_cc_prefix.h"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\cc\windows64\*.dll"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc\windows64"; Components: base; Flags: ignoreversion recursesubdirs signonce
Source: "{#ErgoBaseDir}\cc\windows64\*.exp"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc\windows64"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\cc\windows64\*.lib"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc\windows64"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\cc\C_calling_Ergo\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\cc\C_calling_Ergo"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\pkgs\*.flr"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\pkgs"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\pkgs\include\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\pkgs\include"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\pkgs\prolog\*.P"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\pkgs\prolog"; Components: base; Flags: ignoreversion recursesubdirs 
Source: "{#ErgoBaseDir}\pkgs\.ergo_aux_files\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\pkgs\.ergo_aux_files"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\syslibinc\*.flh"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\syslibinc"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\java\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\java"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

;;;;;;;; Ergo specific subdirs ;;;;;;;;;;

Source: "{#ErgoBaseDir}\ergo_pkgs\.ergo_aux_files\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_pkgs\.ergo_aux_files"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\.ergo_aux_files\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\.ergo_aux_files"; Components: base; Flags: ignoreversion

Source: "{#ErgoBaseDir}\ergo_demos\*"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_demos"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs


;; copy python interface
Source: "{#ErgoBaseDir}\python\*.py"; Excludes: ".*,*~"; DestDir: "{#ergoapp}\python"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\python\*.bat"; Excludes: ".*,*~"; DestDir: "{#ergoapp}\python"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\python\pyergo\*.py"; Excludes: ".*,*~"; DestDir: "{#ergoapp}\python\pyergo"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\python\pyxsb\*.py"; Excludes: ".*,*~"; DestDir: "{#ergoapp}\python\pyxsb"; Components: base; Flags: ignoreversion


;;;; .xwam's are at the end so they will have later time stamps ;;;;;;

Source: "{#ErgoBaseDir}\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\AT\prolog\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\AT\prolog"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\datatypes\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\datatypes"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\debugger\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\debugger"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\syslib\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\syslib"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\ergo_syslib\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_syslib"; Components: base; Flags: ignoreversion touch recursesubdirs 

Source: "{#ErgoBaseDir}\opt\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\opt"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\opt\optstructures.P"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\opt"; Components: base; Flags: ignoreversion touch recursesubdirs 
Source: "{#ErgoBaseDir}\pkgs\prolog\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\pkgs\prolog"; Components: base; Flags: ignoreversion recursesubdirs 

Source: "{#ErgoBaseDir}\ergo_pkgs\evidential_probability\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_pkgs\evidential_probability"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_pkgs\e2dsv\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_pkgs\e2dsv"; Components: base; Flags: ignoreversion

Source: "{#ErgoBaseDir}\ergo_lib\ergo2java\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2java"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\ergo2java\java\*.jar"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2java\java"; Components: base; Flags: ignoreversion 
Source: "{#ErgoBaseDir}\ergo_lib\ergo2json\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2json"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\ergo2sparql\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2sparql"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\ergo2sparql\*.txt"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2sparql"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\ergo2sparql\java\*.jar"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2sparql\java"; Components: base; Flags: ignoreversion 
Source: "{#ErgoBaseDir}\ergo_lib\ergo2owl\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2owl"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\ergo2owl\*.txt"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2owl"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\ergo_lib\ergo2owl\java\*.jar"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\ergo_lib\ergo2owl\java"; Components: base; Flags: ignoreversion 

;; XSB files
Source: "{#XSBBaseDir}\README"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\"; Components: base; Flags: ignoreversion
Source: "{#XSBBaseDir}\LICENSE"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\"; Components: base; Flags: ignoreversion

Source: "{#XSBBaseDir}\bin\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\bin"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\build\*"; Excludes: ".*,CVS,*.cache"; DestDir: "{#xsbapp}\build"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\config\x64-pc-windows\bin\*.exp"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\config\x64-pc-windows\bin"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\config\x64-pc-windows\bin\*.lib"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\config\x64-pc-windows\bin"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\config\x64-pc-windows\bin\*.exe"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\config\x64-pc-windows\bin"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs signonce
Source: "{#XSBBaseDir}\config\x64-pc-windows\bin\*.dll"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\config\x64-pc-windows\bin"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs signonce
Source: "{#XSBBaseDir}\config\x64-pc-windows\*"; Excludes: ".*,bin,CVS,*~"; DestDir: "{#xsbapp}\config\x64-pc-windows"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\pthreads\Pre-built\lib\pthreadVSE1.dll"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\config\i686-pc-cygwin-mt\bin"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs signonce

Source: "{#XSBBaseDir}\syslib\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\syslib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\cmplib\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\cmplib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\lib\*.xwam"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\lib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\prolog-commons\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\prolog-commons"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\prolog_includes\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\prolog_includes"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\emu\*.h"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\emu"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\emu\debugs\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\emu\debugs"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\emu\orastuff\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\emu\orastuff"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#XSBBaseDir}\syslib\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\syslib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\cmplib\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\cmplib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\lib\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\lib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\prolog-commons\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\prolog-commons"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\prolog_includes\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\prolog_includes"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\emu\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\emu"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\etc\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\etc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\gpp\*"; Excludes: ".*,CVS,*~,Makefile"; DestDir: "{#xsbapp}\gpp"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

;; Not distributing XSB manuals any more
;;Source: "{#XSBBaseDir}\docs\userman\*.pdf"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\docs\userman"; Components: documentation; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\examples\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\examples"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#XSBBaseDir}\packages\*"; Excludes: ".*,CVS,*~"; DestDir: "{#xsbapp}\packages"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs


;;;;;;;; ErgoAI specific subdirs ;;;;;;;;;;

;;Source: "{#ErgoAIBaseDir}\*.jar"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoAIapp}"; Components: base; Flags: ignoreversion 

Source: "{#ErgoBaseDir}\Studio_scripts\*.exe"; Excludes: ".*,CVS,*~,install,jena"; DestDir: "{#ergoAIapp}"; Components: base; Flags: ignoreversion signonce
Source: "{#ErgoBaseDir}\Studio_scripts\*.bat"; Excludes: ".*,CVS,*~,install,jena"; DestDir: "{#ergoapp}\Tools"; Components: base; Flags: ignoreversion

;;; OWL/SPARQL tools
Source: "{#ErgoBaseDir}\Tools\jena\*.jar"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\Tools\jena"; Components: base; Flags: ignoreversion 
;;Source: "{#ErgoBaseDir}\Tools\jena\lib\*.jar"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\Tools\jena\lib"; Components: base; Flags: ignoreversion 
Source: "{#ErgoBaseDir}\Tools\jena\ErgoOWL\bin\*.bat"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\Tools\jena\ErgoOWL\bin"; Components: base; Flags: ignoreversion
Source: "{#ErgoBaseDir}\Tools\jena\ErgoSPARQL\bin\*.bat"; Excludes: ".*,CVS,*~"; DestDir: "{#ergoapp}\Tools\jena\ErgoSPARQL\bin"; Components: base; Flags: ignoreversion


[INI]
Filename: "{#ergoAIapp}\{#MyAppUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#MyAppURL}"; Components: base
Filename: "{#ergoAIapp}\{#FlrLicenseUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#FlrLicenseURL}"; Components: base
Filename: "{#ergoAIapp}\{#XSBLicenseUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#XSBLicenseURL}"; Components: base


[Icons]
Name: "{commondesktop}\ErgoAI IDE {#ErgoAIVersion}"; Filename: "{#ergoAIapp}\runErgoAI.exe" ; Comment: "Runs ErgoAI in graphical mode"; WorkingDir: "{userdocs}"; Components: base; IconFilename: "{#ergoapp}\etc\ergo-desktop.ico"; Tasks: shortcut

Name: "{commondesktop}\ErgoAI Engine {#ErgoAIVersion}"; Filename: "cmd"; Parameters: "/k """"{#ergoapp}\runergo.bat""""" ; Comment: "Runs ErgoAI in batch mode"; WorkingDir: "{userdocs}"; Components: base; IconFilename: "{#ergoapp}\etc\ergo-desktop-cmd.ico"; Tasks: shortcut

Name: "{group}\ErgoAI IDE"; Filename: "{#ergoAIapp}\runErgoAI.exe" ; Comment: "Runs ErgoAI in graphical mode"; WorkingDir: "{userdocs}"; Components: base; IconFilename: "{#ergoapp}\etc\ergo.ico"

Name: "{group}\ErgoAI Engine"; Filename: "cmd"; Parameters: "/k """"{#ergoapp}\runergo.bat""""" ; Comment: "Runs ErgoAI in batch mode"; WorkingDir: "{userdocs}"; Components: base; IconFilename: "{#ergoapp}\etc\ergo-desktop-cmd.ico"; Tasks: shortcut

Name: "{group}\ErgoAI License"; Filename: "{#FlrLicenseURL}"; Components: base

Name: "{group}\Web Site"; Filename: "{#MyAppURL}"; Components: base

Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; Components: base

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "ERGOAI_CUST_DIR"; ValueData: "{#ergoAIapp}"; Components: base; Flags: deletevalue uninsdeletevalue

[Run]
; If we are on x64, will use xsb64.bat
Filename: "cmd" ; Parameters: "/c """"{#ergoapp}\ergo_sanity_check.bat"" ""{#xsbapp}\bin\xsb64"""""; WorkingDir: "{#ergoapp}"; Check: Is64BitInstallMode
; Otherwise, will use xsb.bat
Filename: "cmd" ; Parameters: "/c """"{#ergoapp}\ergo_sanity_check.bat"" ""{#xsbapp}\bin\xsb"""""; WorkingDir: "{#ergoapp}"; Check: not Is64BitInstallMode

Filename: "{#ergoAIapp}\{#MyAppUrlName}"; Flags: shellexec nowait; Tasks: website

[UninstallDelete]
Type: filesandordirs; Name: "{#ergoAIapp}"; Components: base
Type: filesandordirs; Name: "{group}"; Components: base



;;; Local Variables: 
;;; eval: (auto-fill-mode 0)
;;; End:
